################################################################################
# The phonological rules of Frantz (1997) as FSTs
################################################################################

# How to understand this file
################################################################################

# - "A -> B || C _ D" means "rewrite A as B only when it occurs between C and D"
# - "(->)" is optional rewrite
# - "define name expression" assigns the FSM/FST generated by "expression" to
#   "name"
# - Special characters (e.g., "-") need to be enclosed in quotes
# - ".o." denotes the composition operation
# - "[..]" denotes the empty symbol in a rewrite rule (using "0" in an insertion
#   rule will result in 1 or more (!) insertions

# Comments
################################################################################

# Some interpretation of the ordered rewrite rules of Frantz (1997) was
# required:
#
# - what to do with the morpheme segmentation symbol "-" in the rules
# - Frantz (1997) provides a partial ordering: some decisions had to be made

# Tests
################################################################################

# A line that begins with "#test" signifies a test.  After "#test" there should
# be a string of characters followed by "->" followed by another string of
# characters.  The first string is the underlying representation and the second
# is the anticipated surface representation.  The OLD can run a phonology script
# against its tests and return a response detailing which tests passed and which
# failed.

#test nit-waanIt-k-wa -> nitaanikka
#test nit-waanIt-aa-wa -> nitaanistaawa
#test nit-siksipawa -> nitssiksipawa
#test nit-ssikópii -> nitsssikópii
#test á-sínaaki-wa -> áísínaakiwa
#test nikáá-ssikópii -> nikáíssikópii
#test káta'-simi-wa -> kátai'simiwa
#test áak-oto-apinnii-wa -> áakotaapinniiwa
#test áak-oto-apinnii-wa -> áakotapinniiwa
#test w-ínni -> ónni
#test w-iihsíssi -> ohsíssi
#test áak-Ipiima -> áaksipiima
#test kitsí'powata-oaawa -> kitsí'powatawaawa
#test á-Io'kaa-wa -> áyo'kaawa
#test yaatóó-t -> aatóót
#test waaníí-t -> aaníít
#test w-óko'si -> óko'si
#test á-yo'kaa-o'pa -> áyo'kao'pa
#test imitáá-iksi -> imitáíksi
#test á-yo'kaa-yi-aawa -> áyo'kaayaawa
#test á-ihpiyi-o'pa -> áíhpiyo'pa
#test á-okstaki-yi-aawa -> áókstakiiyaawa
#test á-okstaki-yi-aawa -> áókstakiyaawa
#test á-okska'si-o'pa -> áókska'so'pa
#test nit-Ioyi -> nitsoyi
#test otokska'si-hsi -> otokska'ssi
#test otá'po'taki-hsi -> otá'po'takssi
#test pii-hsini -> pissini
#test áak-yaatoowa -> áakaatoowa
#test nit-waanii -> nitaanii
#test kikáta'-waaniihpa -> kikáta'waaniihpa
#test áíhpiyi-yináyi -> áíhpiiyináyi
#test áíhpiyi-yináyi -> áíhpiyiyináyi
#test áókska'si-hpinnaan -> áókska'sspinnaan
#test nit-it-itsiniki -> nitsitsitsiniki
#test á'-omai'taki-wa -> áó'mai'takiwa
#test káta'-ookaawaatsi -> kátaookaawaatsi
#test káta'-ottakiwaatsi -> kátaoottakiwaatsi
#test á'-isttohkohpiy'ssi -> áíisttohkohpiy'ssi
#test á'-o'tooyiniki -> áó'tooyiniki
#test káta'-ohto'toowa -> kátao'ohto'toowa
#test káta'-ohto'toowa -> kátaohto'toowa
#test nit-ssksinoawa -> nitssksinoawa
#test á-okska'siwa -> áókska'siwa
#test atsikí-istsi -> atsikíístsi
#test kakkóó-iksi -> kakkóíksi
#test nit-ihpiyi -> nitsspiyi
#test sa-oht-yi -> saohtsi

#test nit-yo'kaa -> nitso'kaa
#test nit-áak-yo'kaa -> nitáakso'kaa
#test nit-áak-ooyi -> nitáaksoyi
#test nit-ooyi -> nitsoyi
#test ooyi -> iiyi
#test nit-yooht-wa -> nitoohtowa
#test nit-yooht-o-aa -> nitsííyoohtoaa
#test nit-yáapi -> nitsaapi
#test nit-yáapi -> nitsíaapi
    
define phonemes [p|t|k|m|n|s|w|y|h|"'"|a|i|o|á|í|ó];
define vowels [a|i|o|á|í|ó];
define accentedVowels [á|í|ó];
define consonants [p|t|k|m|n|s|w|y];
define obstruents [p|t|k|m|n|s];
define stops [p|t|k|m|n];
define plosives [p|t|k];
define glides [w|y];


# 1.    C1-C2     ->     C2C2
# Gemination
define pGem plosives "-" -> p || _ p;
define tGem plosives "-" -> t || _ t;
define kGem plosives "-" -> k || _ k;
define gemination pGem .o. tGem .o. kGem;

# 2.    It    ->    Ist
# s-Insertion (assumes that "breaking I" is a phoneme)
define sInsertion [..] -> s || I _ t;

# 3.a.    C-s    ->    Css
# s-Connection A
define sConnectionA "-" -> s || stops _ s;

# 3.b.    V(')-s    ->    V(')-is
# s-Connection B
# condition: where 's' is not part of a suffix
# present implementation: rule is optional
define sConnectionB [..] (->) i || vowels ("'") "-" _ s;

# 4.    o-a    ->    aa
# o-Replacement
# note: for some speakers the o is deleted
# condition: where 'a' is not part of a suffix
# present implementation: rule is optional
define oReplacementA o (->) [a | 0] || _ "-" a;
define oReplacementB ó (->) [á | 0] || _ "-" a;
define oReplacementC [o | ó] (->) [á | 0] || _ "-" á;
define oReplacement oReplacementA .o. oReplacementB .o. oReplacementC;

# 5.    w-i(i)    ->    o
# Coalescence
define coalescenceA w "-" i (i) -> o || _ [p|t|k|m|n|s|w|y|h|"'"];
define coalescenceB w "-" í (í) -> ó || _ [p|t|k|m|n|s|w|y|h|"'"];
define coalescence coalescenceA .o. coalescenceB;

# 6.    k-I    ->    ksi
# Breaking
define breaking "-" -> s || k _ I;

# 7.    I -> i
# Neutralization
define neutralization I -> i;

# 8.a.    V-iV    ->    VyV
# Desyllabification A
define desyllabificationA "-" i -> y || vowels _ [a|á|o|ó];

# 8.b.    V-oV    ->    VwV
# Desyllabification B
define desyllabificationB "-" o -> w || vowels _ [a|á|i|í|ó];

# 9.    #G    ->    0
# Semivowel Drop
define semivowelDrop glides -> 0 || "#" _;

# 10.    V1V1-V    ->    V1V
# Vowel Shortening
define vowelShorteningA [a | á] -> 0 || [a | á] _ "-" vowels;
define vowelShorteningI [i | í] -> 0 || [i | í] _ "-" vowels;
define vowelShorteningO [o | ó] -> 0 || [o | ó] _ "-" vowels;
define vowelShortening vowelShorteningA .o. vowelShorteningI .o. vowelShorteningO;

# 11.    Vyi-{a,o}    ->    Vy{a,o}
# i-Loss 
define iLossA [i|í] -> 0 || [a|á|o|ó] y _ [a|á|o|ó];
define iLossB i y [i|í] -> i (i) y || _ [a|á|o|ó];
define iLossC í y [i|í] -> í (í) y || _ [a|á|o|ó];
define iLoss iLossA .o. iLossB .o. iLossC;

# 12.    si{a,o}    ->    s{a,o}
# i-Absorption
define iAbsorption [i|í] ("-") -> 0 || s _ [a|á|o|ó];

# 13.    sihs    ->    ss
# ih-Loss
define ihLoss [i|í] "-" h -> 0 || s _ s;

# 14.    ihs    ->    ss
# Presibilation
define presibilation [i|í] "-" h -> s || _ s;

# 15.    CG    ->    C    , where C ne "'"
# Semivowel Loss
define semivowelLossY y -> 0 || [obstruents | I] "-" _;
define semivowelLossW w -> 0 || obstruents "-" _;
define semivowelLoss semivowelLossY .o. semivowelLossW;

# 16.    Ciyiy    ->    Ciiy
# y-Reduction (optional)
define yReduction y (->) 0 || [obstruents | "'"] [i|í] _ [i|í] ("-") y;

# 17.    sih    ->    ss
# Postsibilation
define postsibilation [i|í] ("-") h -> s || s _;

# 18.    ti    ->    tsi
# t-Affrication
define tAffrication "-" -> s || t _ [i|í];

# 19.    V'VC    ->    VV'C
# Glottal Metathesis
define glottalMetathesisA "'" "-" a -> "-" a "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccA "'" "-" á -> "-" á "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisALong "'" "-" a a -> "-" a a "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccALong "'" "-" á á -> "-" á á "'" || vowels _ [consonants|"'"|h];

define glottalMetathesisI "'" "-" i -> "-" i "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccI "'" "-" í -> "-" í "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisILong "'" "-" i i -> "-" i i "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccILong "'" "-" í í -> "-" í í "'" || vowels _ [consonants|"'"|h];

define glottalMetathesisO "'" "-" o -> "-" o "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccO "'" "-" ó -> "-" ó "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisOLong "'" "-" o o -> "-" o o "'" || vowels _ [consonants|"'"|h];
define glottalMetathesisAccOLong "'" "-" ó ó -> "-" ó ó "'" || vowels _ [consonants|"'"|h];

define glottalMetathesis glottalMetathesisA .o. glottalMetathesisAccA .o. 
    glottalMetathesisALong .o. glottalMetathesisAccALong .o. 
    glottalMetathesisI .o. glottalMetathesisAccI .o. glottalMetathesisILong .o. 
    glottalMetathesisAccILong .o. glottalMetathesisO .o. 
    glottalMetathesisAccO .o. glottalMetathesisOLong .o. 
    glottalMetathesisAccOLong;

# 20.    VV1V1'C    ->    VV1V1C
# Glottal Loss
define glottalLossA a a "'" -> a a || vowels ("-") _ consonants;
define glottalLossAccA á á "'" -> á á || vowels ("-") _ consonants;
define glottalLossI i i "'" -> i i || vowels ("-") _ consonants;
define glottalLossAccI í í "'" -> í í || vowels ("-") _ consonants;
define glottalLossO o o "'" -> o o || vowels ("-") _ consonants;
define glottalLossAccO ó ó "'" -> ó ó || vowels ("-") _ consonants;
define glottalLoss glottalLossA .o. glottalLossAccA .o. glottalLossI .o. 
    glottalLossAccI .o. glottalLossO .o. glottalLossAccO;

# 21.    V'(s)CC    ->    VV(s)CC    , where C ne 's'
# Glottal Assimilation
define glottalAssimilationA a "'" -> a a || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationAAcc á "'" -> á á || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationI i "'" -> i i || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationIAcc í "'" -> í í || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationO o "'" -> o o || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilationOAcc ó "'" -> ó ó || _ (s) [p p | t t | k k | m m | n n];
define glottalAssimilation glottalAssimilationA .o. glottalAssimilationAAcc .o. 
    glottalAssimilationI .o. glottalAssimilationIAcc .o. glottalAssimilationO .o. 
    glottalAssimilationOAcc;

# 22.    ''    ->    '
# Glottal Reduction
define glottalReduction "'" "'" -> "'";

# 23.    V1'h    ->    V1'V1h
# Vowel Epenthesis
# note: In place of this rule, some speakers have the following rule:
# ' -> 0 / _ h
define vowelEpenthesisA a "'" -> [a "'" a | a] || _ h;
define vowelEpenthesisAAcc á "'" -> [á "'" á | á] || _ h;
define vowelEpenthesisI i "'" -> [i "'" i | i] || _ h;
define vowelEpenthesisIAcc í "'" -> [í "'" í | í] || _ h;
define vowelEpenthesisO o "'" -> [o "'" o | o] || _ h;
define vowelEpenthesisOAcc ó "'" -> [ó "'" ó | ó] || _ h;
define vowelEpenthesis vowelEpenthesisA .o. vowelEpenthesisAAcc .o. 
    vowelEpenthesisI .o. vowelEpenthesisIAcc .o. 
    vowelEpenthesisO .o. vowelEpenthesisOAcc;

# 24.    sssC    ->    ssC
# sss-Shortening
define sssShortening s -> 0 || _ s s [stops | glides];

# 25.
# Accent Spread
define accentSpreadA a -> á || accentedVowels "-" _;
define accentSpreadI i -> í || accentedVowels "-" _;
define accentSpreadO o -> ó || accentedVowels "-" _;
define accentSpread accentSpreadO .o. accentSpreadA .o. accentSpreadI;

# 26.    - -> 0
# Break-Delete
define breakDelete "-" -> 0;

# 27.
# Deletion of Non-permanent Consonants (cf. Frantz 1997: 9)
#  These consonants disappear before vowels, see p. 82 of grammar.
define deleteNonPermanentConsonants N|M|S -> 0 || _ "-" vowels;
define neutralizeNonPermanentConsonantN N -> n || _ ["-" | "#"];
define neutralizeNonPermanentConsonantM M -> m || _ ["-" | "#"];
define neutralizeNonPermanentConsonantS S -> s || _ ["-" | "#"];
define neutralizeNonPermanentConsonants neutralizeNonPermanentConsonantN .o. 
    neutralizeNonPermanentConsonantM .o. neutralizeNonPermanentConsonantS;
define resolveNonPermanentConsonants deleteNonPermanentConsonants .o. 
    neutralizeNonPermanentConsonants;

# 28.
# Inverse o-Loss ("t-ok-" -> "t-k-")
define oLossINV o -> 0 || t "-" _ k "-";

# 29.
# nasal-Loss (might be a more general rule...)
# define nasalLoss m|n -> 0 || plosives "-" _;

define phonology oLossINV .o. 
    resolveNonPermanentConsonants .o. 
    semivowelLoss .o. 
    gemination .o. 
    coalescence .o. 
    sInsertion .o. 
    sConnectionB .o. 
    yReduction .o. 
    breaking .o. 
    oReplacement .o. 
    ihLoss .o. 
    sConnectionA .o. 
    presibilation .o. 
    sssShortening .o. 
    semivowelDrop .o. 
    vowelShortening .o.  
    neutralization .o. 
    tAffrication .o. 
    postsibilation .o. 
    iAbsorption .o.
    desyllabificationB .o. 
    desyllabificationA .o. 
    glottalMetathesis .o. 
    vowelEpenthesis .o. 
    glottalReduction .o. 
    glottalLoss .o. 
    glottalAssimilation .o. 
    accentSpread .o. 
    breakDelete .o. 
    iLoss;